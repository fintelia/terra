// Based on: https://github.com/wwwtyro/glsl-atmosphere/blob/master/index.glsl
#define PI 3.141592
#define iSteps 4
#define jSteps 4

uniform float planetRadius;
uniform float atmosphereRadius;

uniform sampler2D transmittance;
uniform sampler3D inscattering;

vec2 rsi(vec3 r0, vec3 rd, float sr) {
    // ray-sphere intersection that assumes
    // the sphere is centered at the origin.
    // No intersection when result.x > result.y
    float a = dot(rd, rd);
    float b = 2.0 * dot(rd, r0);
    float c = dot(r0, r0) - (sr * sr);
    float d = (b*b) - 4.0*a*c;
    if (d < 0.0) return vec2(1e5,-1e5);
    return vec2(
        (-b - sqrt(d))/(2.0*a),
        (-b + sqrt(d))/(2.0*a)
    );
}

vec3 atmosphere(vec3 r0, vec3 r1, vec3 pSun) {
	float iSun = 22.0;
	vec3 kRlh = vec3(5.5e-6, 13.0e-6, 22.4e-6);
	float kMie = 21e-6;
	float shRlh = 8e3;
	float shMie = 1.2e3;
	float g = 0.758;

    // Normalize the sun and view directions.
    vec3 r = normalize(r1 - r0);

	// Primary ray initialization.
    float iStepSize = distance(r0, r1) / float(iSteps);
    float iTime = 0.0;

    // Initialize accumulators for Rayleigh and Mie scattering.
    vec3 totalRlh = vec3(0,0,0);
    vec3 totalMie = vec3(0,0,0);

    // Initialize optical depth accumulators for the primary ray.
    float iOdRlh = 0.0;
    float iOdMie = 0.0;

    // Calculate the Rayleigh and Mie phases.
    float mu = dot(r, pSun);
    float mumu = mu * mu;
    float gg = g * g;
    float pRlh = 3.0 / (16.0 * PI) * (1.0 + mumu);
    float pMie = 3.0 / (8.0 * PI) * ((1.0 - gg) * (mumu + 1.0)) / (pow(1.0 + gg - 2.0 * mu * g, 1.5) * (2.0 + gg));

    // Sample the primary ray.
    for (int i = 0; i < iSteps; i++) {

        // Calculate the primary ray sample position.
        vec3 iPos = r0 + r * (iTime + iStepSize * 0.5);

        // Calculate the height of the sample.
        float iHeight = length(iPos + vec3(0,planetRadius,0)) - planetRadius;

        // Calculate the optical depth of the Rayleigh and Mie scattering for this step.
        float odStepRlh = exp(-iHeight / shRlh) * iStepSize;
        float odStepMie = exp(-iHeight / shMie) * iStepSize;

        // Accumulate optical depth.
        iOdRlh += odStepRlh;
        iOdMie += odStepMie;

        // Calculate the step size of the secondary ray.
        float jStepSize = rsi(iPos + vec3(0,planetRadius,0), pSun, atmosphereRadius).y / float(jSteps);

        // Initialize the secondary ray time.
        float jTime = 0.0;

        // Initialize optical depth accumulators for the secondary ray.
        float jOdRlh = 0.0;
        float jOdMie = 0.0;

        // Sample the secondary ray.
        for (int j = 0; j < jSteps; j++) {

            // Calculate the secondary ray sample position.
            vec3 jPos = iPos + pSun * (jTime + jStepSize * 0.5);

            // Calculate the height of the sample.
            float jHeight = length(jPos + vec3(0,planetRadius,0)) - planetRadius;

            // Accumulate the optical depth.
            jOdRlh += exp(-jHeight / shRlh) * jStepSize;
            jOdMie += exp(-jHeight / shMie) * jStepSize;

            // Increment the secondary ray time.
            jTime += jStepSize;
        }

        // Calculate attenuation.
        vec3 attn = exp(-(kMie * (iOdMie + jOdMie) + kRlh * (iOdRlh + jOdRlh)));

        // Accumulate scattering.
        totalRlh += odStepRlh * attn;
        totalMie += odStepMie * attn;

        // Increment the primary ray time.
        iTime += iStepSize;

    }

    // Calculate and return the final color.
    return iSun * (pRlh * kRlh * totalRlh + pMie * kMie * totalMie);
}

vec3 aerial_perspective(vec3 color, vec3 position, vec3 cameraPosition, vec3 sunDirection) {
	vec3 air = atmosphere(cameraPosition, position, sunDirection);
    return 1.0 - exp(-0.75 * air) + color;
}

void reverse_parameters(float r, float mu, float mu_s, float v,
						out float u_r, out float u_mu, out float u_mu_s, out float u_v) {
	float H = sqrt(atmosphereRadius * atmosphereRadius - planetRadius * planetRadius);
	float rho = sqrt(r * r - planetRadius * planetRadius);
	float delta = r * r * mu * mu - rho * rho;

	u_r = rho / H;

	if(mu < 0.0 && delta >= 0.0){
		// Ray intersects planet surface.
		float d_min = r - planetRadius;
		float d_max = rho;
		float d = -r * mu - sqrt(delta);
		if(d_min == d_max){
			u_mu = 0.0;
		} else {
			u_mu = 0.5 - 0.5 * (d - d_min) / (d_max - d_min);
		}
	} else {
		float d_min = atmosphereRadius - r;
		float d_max = rho + H;
		float d = -r * mu + sqrt(delta + H * H);

		u_mu = 	0.5 + 0.5 * (d - d_min) / (d_max - d_min);
	}

	u_mu_s = (1.0 - exp(-3.0 * mu_s - 0.6)) / (1.0 - exp(-3.6));
	u_v = (1.0 + v) / 2.0;
}

vec4 texture4D(sampler3D sampler, vec4 P, int slices) {
	ivec3 size = textureSize(sampler, 0);
	vec2 Pxy = (P.xy * (size.xy-1) + 0.5) / size.xy;

	float fw = floor(P.w*slices);

	vec3 P1 = vec3(Pxy, ((P.z / slices + fw / slices) * (size.z-1) + 0.5) / size.z);
	vec3 P2 = vec3(Pxy, ((P.z / slices + (fw+1) / slices) * (size.z-1) + 0.5) / size.z);

	return mix(texture(sampler,P1), texture(sampler,P2), (P.w*slices - fw));
}

vec3 precomputed_transmittance(float r, float v) {
	vec2 size = textureSize(transmittance, 0);
	vec2 P = vec2((r - planetRadius) / (atmosphereRadius - planetRadius), v * 0.5 + 0.5);
	return texture(transmittance, (P * (size-1) + 0.5) / size).rgb;
}

vec3 precomputed_transmittance2(vec3 x, vec3 y) {
	x.y += planetRadius;
	y.y += planetRadius;
	float v = normalize(y - x).y;
	return precomputed_transmittance(length(x), v) / precomputed_transmittance(length(y), v);
}

vec3 precomputed_atmosphere(vec3 x, vec3 x0, vec3 sun_normalized) {
	vec3 v_normalized = normalize(x0 - x);
	vec3 x_normalized = normalize(x);

	float r = length(x + vec3(0, planetRadius, 0));
	float mu = dot(v_normalized, x_normalized);
	float mu_s = dot(sun_normalized, x_normalized);
	float v = dot(v_normalized, sun_normalized);

	float u_r, u_mu, u_mu_s, u_v;
	reverse_parameters(r, mu, mu_s, v, u_r, u_mu, u_mu_s, u_v);

	return texture4D(inscattering, vec4(u_r, u_mu, u_mu_s, u_v), 8).rgb;
}

vec3 precomputed_aerial_perspective(vec3 color, vec3 position, vec3 cameraPosition,
									vec3 sunDirection) {
	vec3 x = normalize(position + vec3(0, planetRadius, 0)) * planetRadius - vec3(0,planetRadius,0);
	vec3 t = precomputed_transmittance2(x, cameraPosition);

	return color * t + vec3(1) * (1 - t);
}
